1) All Channel Sales
====================
All Channel Sales = 
SUM('Internet Sales'[Internet Sales]) + SUM('Reseller Sales'[Reseller Sales])

2) Blue Revenue
===============
Blue Revenue = CALCULATE('Measures Table'[All Channel Sales],'Product'[Color]="Blue")

3) Revenue Red or Blue
======================
Revenue Red or Blue = 
CALCULATE('Measures Table'[All Channel Sales], 'Product'[Color] IN {"Red", "Blue"})

4) Premium Revenue
==================
Premium Revenue = 
CALCULATE('Measures Table'[All Channel Sales], 'Product'[List Price] > 2000)

5) Blue Revenue DAX Rounded 
==========================
Blue Revenue DAX ROUNDUP = ROUNDUP( 
    CALCULATE('Measures Table'[All Channel Sales],'Product'[Color]="Blue"),
    0)


6) Sales All Country
=====================
Sales All Country = 
	CALCULATE(SUM('Reseller Sales'[Reseller Sales]),
		REMOVEFILTERS(Region) )

7) Sales % All Country
======================
Sales % All Country = 
DIVIDE(
	SUM('Reseller Sales'[Reseller Sales]),
	CALCULATE(
		SUM('Reseller Sales'[Reseller Sales]),
		REMOVEFILTERS(Region)
	)
)

8) Sales % Region (version 1)
=================================
Sales % Region V1 = 
DIVIDE(
	SUM('Reseller Sales'[Reseller Sales]),
	CALCULATE(
		SUM('Reseller Sales'[Reseller Sales]),
		REMOVEFILTERS('Region'[Country])
	)
	)


9)Sales % Region (Improved Version)
==================================

Sales % Region = 
IF(
	ISINSCOPE(Region[Country]),
	DIVIDE(
		SUM('Reseller Sales'[Reseller Sales]),
		CALCULATE(
			SUM('Reseller Sales'[Reseller Sales]),
			REMOVEFILTERS(Region[Country])
	)
	)
)



10) Sales % Group
==================
Sales % Group = 
IF(
    ISINSCOPE(Region[Region]) || ISINSCOPE(Region[Country]),
    DIVIDE(
	    SUM('Reseller Sales'[Reseller Sales]),
	    CALCULATE(
		    SUM('Reseller Sales'[Reseller Sales]),
		    REMOVEFILTERS(
                	Region[Region],
                	Region[Country]
                    )

            )
    )
)

11) Tax Rate Column
=================

Tax Rate = 
LOOKUPVALUE
	('VAT Rate'[Tax Rate], 	
	'VAT Rate'[Tax Code], 	
	'Reseller Sales'[Tax Code])

12) Sales Year ToDate
=====================
Sales YTD = 
TOTALYTD(SUM('Reseller Sales'[Reseller Sales]), 'Date'[Date], "3-31")

13)Sales Previous Year
======================
Sales PY = 
	CALCULATE(
		SUM('Reseller Sales'[Reseller Sales]),
		SAMEPERIODLASTYEAR(
			'Date'[Date]
		))


14) Sales 2 Mths Prior
====================== 
Sales 2 Mths Prior = 
 CALCULATE(SUM('Reseller Sales'[Reseller Sales]), 
    PARALLELPERIOD('Date'[Date],-2,MONTH))


15) Sales Year On Year Growth % - Introduce Use of Variables
============================================================
Sales YoY Growth = 
VAR SalesPriorYear =
	CALCULATE(
		SUM('Reseller Sales'[Reseller Sales]),
		PARALLELPERIOD(
			'Date'[Date],
			-12,
			MONTH
		)
	)
RETURN
	SalesPriorYear

16)Sales Year On Year Growth % (Complete)
=========================================
Sales YoY Growth % = 
VAR SalesPriorYear =
	CALCULATE(
		SUM('Reseller Sales'[Reseller Sales]),
		PARALLELPERIOD(
			'Date'[Date],
			-12,
			MONTH
		)
	)
RETURN
	DIVIDE(
		(SUM('Reseller Sales'[Reseller Sales]) - SalesPriorYear),
		SalesPriorYear
	)

17) Difference between SUM and SUMX
====================================

17.1) 

Reseller Sales After Fixed Rate Tax = 
	SUM('Reseller Sales'[Reseller Sales]) *0.9

17.2)

Reseller Sales A/F Country Specific Tax = 
	SUMX('Reseller Sales', 'Reseller Sales'[Reseller Sales] 
		* ( 1 - 'Reseller Sales'[Tax Rate] ))

18) Average Variation Days
==========================

Avg Late Delivery Days = 
 	AVERAGEX('Reseller Sales', 
	DATEDIFF('Reseller Sales'[DueDate], 'Reseller Sales'[DeliveryDate], DAY) )

19) No of Work Days
===================
No of Workdays = 
    VAR startDate = SELECTEDVALUE('Reseller Sales'[OrderDate])
    VAR endDate = SELECTEDVALUE('Reseller Sales'[DeliveryDate])
    RETURN
    NETWORKDAYS(startDate,endDate,1,'Holiday list')





